.interp         : { *(.interp) } /*RO DATA*/
.note.gnu.build-id : { *(.note.gnu.build-id) } /*RO DATA*/
.note.ABI-tag   : { *(.note.ABI-tag) } /*RO DATA*/
.hash           : { *(.hash) } /*RO DATA*/ 
.gnu.hash       : { *(.gnu.hash) } /*RO DATA*/
.dynsym         : { *(.dynsym) } /*RO DATA*/ 
.dynstr         : { *(.dynstr) } /*RO DATA*/
.gnu.version    : { *(.gnu.version) } /*RO DATA*/
.gnu.version_d  : { *(.gnu.version_d) } /*RO DATA*/
.gnu.version_r  : { *(.gnu.version_r) } /*RO DATA*/
.rela.init      : { *(.rela.init) } /*RO DATA*/
.rela.text      : { *(.rela.text .rela.text.* .rela.gnu.linkonce.t.*) } /*RO DATA*/
.rela.fini      : { *(.rela.fini) } /*RO DATA*/
.rela.rodata    : { *(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*) } /*RO DATA*/
.rela.data.rel.ro   : { *(.rel.data.rel.ro*) } /*RO DATA*/
.rela.data      : { *(.rela.data .rela.data.* .rela.gnu.linkonce.d.*) } /*RO DATA*/
.rela.tdata	  : { *(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*) } /*RO DATA*/
.rela.tbss	  : { *(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*) } /*RO DATA*/
.rela.ctors     : { *(.rela.ctors) } /*RO DATA*/
.rela.dtors     : { *(.rela.dtors) } /*RO DATA*/
.rela.dyn       : { *(rela.dyn) *(.rela__libc_subfreeres) *(.rela__libc_atexit) *(.rela__libc_thread_subfreeres) *(.rela.init_array) *(.rela.fini_array) } /*RO DATA*/
.rela.got       : { *(.rela.got) } /*RO DATA*/
.rela.sdata     : { *(.rela.sdata .rela.sdata.* .rela.gnu.linkonce.s.*) } /*RO DATA*/
.rela.sbss      : { *(.rela.sbss .rela.sbss.* .rela.gnu.linkonce.sb.*) } /*RO DATA*/
.rela.sdata2    : { *(.rela.sdata2 .rela.sdata2.* .rela.gnu.linkonce.s2.*) } /*RO DATA*/
.rela.sbss2     : { *(.rela.sbss2 .rela.sbss2.* .rela.gnu.linkonce.sb2.*) } /*RO DATA*/
.rela.bss       : { *(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*) } /*RO DATA*/
.rela.plt       : { *(.rela.plt) } /*RO DATA*/
.init           :
{
  KEEP (*(.init))
}  /*RO CODE*/
.text           :
{
  *(.text .stub .text.* .gnu.linkonce.t.*)
  KEEP (*(.text.*personality*))
  *(.gnu.warning)
  . = ALIGN (4);
} =0x0100 /*RO CODE*/
.fini           :
{
  KEEP (*(.fini))
}  /*RO CODE*/
.exec.itable     : { *(.exec.itable) } /*RO CODE*/
PROVIDE (__etext = .); /* END */
PROVIDE (_etext = .); /* END */
PROVIDE (etext = .); /* END */
.rodata         : { *(.rodata .rodata.* .gnu.linkonce.r.*) } /*RO DATA*/
.rodata1        : { *(.rodata1) } /*RO DATA*/
.sdata2         : { *(.sdata2 .sdata2.* .gnu.linkonce.s2.*) } /*RO DATA*/
.sbss2          : { *(.sbss2 .sbss2.* .gnu.linkonce.sb2.*) } /*RO DATA*/
.eh_frame_hdr : { *(.eh_frame_hdr) } /*RO DATA*/ 
. = ALIGN (0x20); /* RW START*/
. = DATA_SEGMENT_ALIGN (0x20, 0x20); /* RW START*/
.eh_frame       : { KEEP (*(.eh_frame)) } /*RW DATA*/
.gcc_except_table   : { KEEP (*(.gcc_except_table)) *(.gcc_except_table.*) } /*RW DATA*/
.tdata	  : { *(.tdata .tdata.* .gnu.linkonce.td.*) } /*RW DATA*/
.tbss		  : { *(.tbss .tbss.* .gnu.linkonce.tb.*) *(.tcommon) } /*ZI*/
. = ALIGN(8); /* START */
.preinit_array     : {
  PROVIDE (__preinit_array_start = .);
  KEEP (*(.preinit_array))
  PROVIDE (__preinit_array_end = .);
} /*RW DATA*/
.init_array     : {
  PROVIDE (__init_array_start = .);
  KEEP (*(SORT_BY_INIT_PRIORITY(.init_array.*)))
  KEEP (*(.init_array))
  PROVIDE (__init_array_end = .);
} /*RW DATA*/
.fini_array     : {
  PROVIDE (__fini_array_start = .);
  KEEP (*(SORT_BY_INIT_PRIORITY(.fini_array.*)))
  KEEP (*(.fini_array))
  PROVIDE (__fini_array_end = .);
} /*RW DATA*/
.tm_clone_table : { *(.tm_clone_table) } /*RW DATA*/
.ctors          :
{
  KEEP (*crtbegin*.o(.ctors))
  KEEP (*(EXCLUDE_FILE (*crtend*.o ) .ctors))
  KEEP (*(SORT(.ctors.*)))
  KEEP (*(.ctors))
} /*RW DATA*/
.dtors          :
{
  KEEP (*crtbegin*.o(.dtors))
  KEEP (*(EXCLUDE_FILE (*crtend*.o ) .dtors))
  KEEP (*(SORT(.dtors.*)))
  KEEP (*(.dtors))
} /*RW DATA*/
.jcr            : { KEEP (*(.jcr)) } /*RW DATA*/
.data.rel.ro : { *(.data.rel.ro.local) *(.data.rel.ro*) } /*RW DATA*/
.dynamic        : { *(.dynamic) } /*RW DATA*/
. = ALIGN(8); /* START */
.got            : { *(.got.plt) *(.got) } /*RW DATA*/
.data           :
{
  *(.data .data.* .gnu.linkonce.d.*)
  KEEP (*(.gnu.linkonce.d.*personality*))
  SORT(CONSTRUCTORS)
  . = ALIGN(8);
} /*RW DATA*/
.data1          : 
{ 
  *(.data1) 
  . = ALIGN(8);
} /*RW DATA*/
. = ALIGN(8); /* START */
.sdata          :
{
  *(.srodata.cst16)
  *(.srodata.cst8)
  *(.srodata.cst4)
  *(.srodata.cst2)
  *(.srodata .srodata.*)
  *(.sdata .sdata.* .gnu.linkonce.s.*)
} /*RW DATA*/
.sdata_d          :
{
  *(.sdata_d .sdata_d.*)
} /*RW DATA*/
.sdata_w          :
{
  *(.sdata_w .sdata_w.*)
} /*RW DATA*/
.sdata_h          :
{
  *(.sdata_h .sdata_h.*)
} /*RW DATA*/
.sdata_b          :
{
  *(.sdata_b .sdata_b.*)
} /*RW DATA*/
.sdata_f          :
{
  *(.sdata_f .sdata_f.*)
} /*RW DATA*/
. = ALIGN(8); /* END */
_edata = .; /* RW END */
PROVIDE (edata = .); /* RW END */
__bss_start = .; /* ZI START */
PROVIDE (__sbss_start = .); /* ZI START */
PROVIDE (___sbss_start = .); /* ZI START */
.sbss           :
{
  *(.dynsbss)
  *(.sbss .sbss.* .gnu.linkonce.sb.*)
  *(.scommon .scommon.*)
} /*ZI*/
.sbss_f           :
{
  *(.sbss_f .sbss_f.*)
  *(.scommon_f .scommon_f.*)
} /*ZI*/
.sbss_b           :
{
  *(.sbss_b .sbss_b.*)
  *(.scommon_b .scommon_b.*)
  . = ALIGN(2);
} /*ZI*/
.sbss_h           :
{
  *(.sbss_h .sbss_h.*)
  *(.scommon_h .scommon_h.*)
  . = ALIGN(4);
} /*ZI*/
.sbss_w           :
{
  *(.sbss_w .sbss_w.*)
  *(.scommon_w .scommon_w.*)
  *(.dynsbss)
  *(.scommon)
  . = ALIGN(8);
} /*ZI*/
.sbss_d           :
{
  *(.sbss_d .sbss_d.*)
  *(.scommon_d .scommon_d.*)
} /*ZI*/
PROVIDE (__sbss_end = .); /* ZI END */
PROVIDE (___sbss_end = .); /* ZI END */ 
.bss            :
{
 *(.dynbss)
 *(.bss .bss.* .gnu.linkonce.b.*)
 *(COMMON)
 . = ALIGN(8);
} /*ZI*/
. = ALIGN(8);/* ZI END */
_end = .; /* ZI END */
. = DATA_SEGMENT_END (.); /* ZI END */
PROVIDE (end = .); /* ZI END */
.nds_vector   :
{
  KEEP (*(.nds_vector))
  KEEP (*(SORT(.nds_vector.*)))
} /*ISR*/
